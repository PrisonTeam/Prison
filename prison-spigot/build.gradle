/*
 * Prison is a Minecraft plugin for the prison game mode.
 * Copyright (C) 2018 The Prison Team
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

group 'tech.mcprison'

repositories {
    mavenCentral()
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url = "http://nexus.hc.to/content/repositories/pub_releases" }
    maven { url = "https://repo.lucko.me/" }
    maven { url = "http://repo.bstats.org/content/repositories/releases/" }
    maven { url = "https://repo.inventivetalent.org/content/groups/public/" }
    maven { url = "http://repo.mvdw-software.be/content/groups/public/" }
    jcenter()
}

dependencies {
    compile project(':prison-core')
    compile project(':prison-mines')
    compile project(':prison-ranks')
    compile 'org.bstats:bstats-bukkit:1.2'
    compile 'org.inventivetalent.spiget-update:bukkit:1.4.1'

    compileOnly 'org.spigotmc:spigot-api:1.9.4-R0.1-SNAPSHOT'
    compileOnly 'net.milkbowl.vault:VaultAPI:1.6'
    compileOnly 'me.lucko.luckperms:luckperms-api:4.0'
    compileOnly('be.maximvdw:MVdWPlaceholderAPI:2.4.0-SNAPSHOT'){
        exclude group: 'org.spigotmc'
    }
    compileOnly fileTree(dir: 'lib', include: ['*.jar'])
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'version': project.version
        include 'plugin.yml'
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

shadowJar {
    dependencies {
        include(dependency('com.google.code.gson:gson:2.7'))
        include(dependency('com.fasterxml.jackson.core:jackson-databind:2.7.0'))
        include(dependency('com.fasterxml.jackson.core:jackson-core:2.7.0'))
        include(dependency('com.fasterxml.jackson.core:jackson-annotations:2.7.0'))
        include(dependency('org.bstats:bstats-bukkit:1.2'))
        include(dependency('org.inventivetalent.spiget-update:bukkit:1.4.1'))
        include(project(':prison-core'))
        include(project(':prison-mines'))
        include(project(':prison-ranks'))
    }

    relocate 'org.bstats.bukkit', 'tech.mcprison.prison.spigot'

    classifier ''
    version = null
}

build.dependsOn(shadowJar)
